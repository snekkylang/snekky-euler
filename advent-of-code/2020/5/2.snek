let input = File.read("./input.txt")
    .split("\r\n")
    .map(func(v) {
        v.split("")
    });

let Position = func(rowMin, rowMax, colMin, colMax) {
    return {
        rowMin: rowMin,
        rowMax: rowMax,
        colMin: colMin,
        colMax: colMax,
    };
};

let solve = func() {
    let seats = [];

    for let t in input {
        mut pos = Position(0, 127, 0, 7);

        for let d in t {            
            pos = when d {
                "F" => {
                    let half = Math.floor((pos.rowMin + pos.rowMax) / 2);

                    Position(pos.rowMin, half, pos.colMin, pos.colMax)
                }
                "B" => {
                    let half = Math.floor((pos.rowMin + pos.rowMax) / 2);

                    Position(half + 1, pos.rowMax, pos.colMin, pos.colMax)  
                }
                "R" => {
                    let half = Math.floor((pos.colMin + pos.colMax) / 2);

                    Position(pos.rowMin, pos.rowMax, half + 1, pos.colMax)  
                }
                "L" => {
                    let half = Math.floor((pos.colMin + pos.colMax) / 2);

                    Position(pos.rowMin, pos.rowMax, pos.colMin, half)  
                }
            };
        }

        let seatId = pos.rowMin * 8 + pos.colMin;
        seats.push(seatId);
    }

    seats.sort(func(v1, v2) {
        v1 - v2
    });
    
    mut i = 0;
    while i < seats.length() - 1 {
        if seats[i] != seats[i + 1] - 1 {
            return seats[i] + 1;
        }

        i += 1;
    }
};

Sys.println(solve());