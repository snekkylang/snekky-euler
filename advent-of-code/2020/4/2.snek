let input = File.read("./input.txt")
    .split("\n\n")
    .map(func(v) {
        v.replace("\n", " ")
    })
    .map(func(v) {
        let o = {};
        for let pair in v.split(" ") {
            let [key, value] = pair.split(":");
            o[key] = value;
        }

        o
    });

let solve = func() {
    mut valid = 0;

    for let pp in input {
        let {byr, iyr, eyr, hgt, hcl, ecl, pid} = pp;

        if !~/^(19[2-9][0-9]|200[0-2])$/.match(byr.toString()) {
            continue;
        }

        if !~/^(201[0-9]|2020)$/.match(iyr.toString()) {
            continue;
        }

        if !~/^(202[0-9]|2030)$/.match(eyr.toString()) {
            continue;
        }

        if !~/^((1([5-8][0-9]|9[0-3])cm)|((59|6[0-9]|7[0-6])in))$/.match(hgt.toString()) {
            continue;
        }

        if !~/^#[0-9a-f]{6}$/.match(hcl.toString()) {
            continue;
        }

        if !~/^(amb|blu|brn|gry|grn|hzl|oth)$/.match(ecl.toString()) {
            continue;
        }

        if !~/^[0-9]{9}$/.match(pid.toString()) {
            continue;
        }

        valid += 1;
    }

    return valid;
};

Sys.println(solve());